group = 'rocks.revolt'

ext {
    artifactId = 'revolt-sdk-android'

    pubName = 'Revolt Android SDK'
    pubDescription = 'Revolt Android SDK'

    siteUrl = 'https://www.revolt.rocks'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

def pomConfig = {
    licenses {
        license {
            name licenseName
            url licenseUrl
            distribution "repo"
        }
    }
    developers {
        developer {
            id "damiandzierzak"
            name "Damian Dzierzak"
            email "damian.dzierzak@miquido.com"
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

apply plugin: 'digital.wup.android-maven-publish'
publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.android
            groupId project.group
            artifactId project.artifactId
            version '1.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.pubName)
                root.appendNode('description', project.pubDescription)
                root.children().last() + pomConfig
            }
        }
    }
}

// Bintray
apply plugin: 'com.jfrog.bintray'
bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.apiKey') ? project.property('bintray.apiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenAar']
    publish = true
    pkg {
        userOrg = 'miquido'
        repo = 'maven-repo'
        name = project.artifactId
        desc = project.description
        websiteUrl = siteUrl
        licenses = allLicenses
        publicDownloadNumbers = true
        githubReleaseNotesFile = 'README.md'
        version {
            name = '1.0.1'
            released = new Date()
            vcsTag = 'version-' + '1.0.1'
        }
    }
}
